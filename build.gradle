buildscript {
  repositories {
    jcenter()
    maven { url "http://dl.bintray.com/spinnaker/gradle" }
  }
  dependencies {
    classpath 'com.netflix.spinnaker:spinnaker-gradle-project:1.12.115'
  }
}

group = "com.netflix.spinnaker.spinnaker"


apply plugin: 'application'
apply plugin: 'nebula.os-package'
apply plugin: 'nebula.os-package-base'



ospackage {

// common installation paths 
  def spinnakerHome = "/opt/" + project.applicationName

// global package requirements
  packageName = project.applicationName
  version = project.version.replaceAll("-SNAPSHOT", "")
  release '3'

// installer scripts
  preInstall = file('scripts/preInstall.sh')
  postInstall = file('scripts/postInstall.sh')
  postUninstall = file('scripts/postUninstall.sh')

// files to put in the meta package
  from('pylib') {
    into spinnakerHome + '/pylib'
  }

  from('cassandra') {
    into spinnakerHome + '/cassandra'
  }

  from('config') {
    into spinnakerHome + '/config'
  }

  from('runtime/reconfigure_spinnaker.sh') {
    into spinnakerHome + '/bin'
    fileMode = 0755
  }

}


// Ubuntu
buildDeb {
  requires('python-pip')
  requires('openjdk-8-jre', '8u45-b14-1~14.04', EQUAL)
  //requires('cassandra', '2.1.9', EQUAL)
  requires('cassandra-tools', '2.1.9', EQUAL)
  requires('dsc21', '2.1.9-1', EQUAL)
  requires('redis-server', '3.0.5', GREATER | EQUAL)
  requires('clouddriver')
  requires('deck')
  requires('echo')
  requires('front50')
  requires('gate')
  requires('igor')
  requires('orca')
  requires('rosco')
  requires('rush')
  requires('apache2')
  
}


// TO DO 
buildRpm {
  arch = NOARCH
  os = LINUX
}
